model{

  ######################
  for(j in 1:m){
    for(t in 1:T){
      for(k in 1:4){
        mu.jtk[j,t,k] <- b.sex.tk[sex.j[j,t],t,k] + b.age.tk[age.j[j,t],t,k] + b.educ.tk[educ.j[j,t],t,k] +beta0.tk[t,k] 
        emu[j,t,k] <- exp(mu.jtk[j,t,k])
        p[j,t,k] <- emu[j,t,k]/sum(emu[j,t,1:4])
      }
      y.jt[j,t] ~ dcat(p[j,t,1:4])
    }
    
  }
  
  for(t in 1:T){
    beta0.tk[t,1] <-0
      for (s in 1:n.sex){
        b.sex.tk[s,t,1] <-0
      }
      for (a in 1:n.age){
        b.age.tk[a,t,1] <-0
      }
      for (e in 1:n.educ){
        b.educ.tk[e,t,1]<-0
      }
    for(k in 2:4){
      beta0.tk[t,k] ~ dnorm(beta0.k[k], tau.beta.k[k])
      for (s in 1:n.sex){
        b.sex.tk[s,t,k] ~ dnorm(b.sex.k[s,k], tau.sex.m.sk[s,k])
      }
      for (a in 1:n.age){
        b.age.tk[a,t,k] ~ dnorm(b.age.k[a,k], tau.age.m.ak[a,k])
      }
      for (e in 1:n.educ){
        b.educ.tk[e,t,k] ~ dnorm(b.educ.k[e,k], tau.educ.m.ek[e,k])
      }
    }
      
  }
  for(k in 2:4){
    beta0.k[k] ~ dnorm(0,0.01)
    
    for (s in 1:n.sex){
      b.sex.k[s,k] ~ dnorm(0, tau.sex.mk[k])
      tau.sex.m.sk[s,k] ~ dchisq(nu.sex.m.sk[s,k])
      nu.sex.m.sk[s,k] ~ dunif(0,100)
    }
    for (a in 1:n.age){
      b.age.k[a,k] ~ dnorm(0, tau.age.mk[k])
      tau.age.m.ak[a,k] ~ dchisq(nu.age.m.ak[a,k])
      nu.age.m.ak[a,k] ~ dunif(0,100)
    }
    for (e in 1:n.educ){
      b.educ.k[e,k] ~ dnorm(0, tau.educ.mk[k])
      tau.educ.m.ek[e,k] ~ dchisq(nu.educ.m.ek[e,k])
      nu.educ.m.ek[e,k] ~ dunif(0,100)
    }
    tau.beta.k[k] ~ dchisq(nu.beta[k])
    nu.beta[k] ~ dunif(0,100)
    tau.sex.mk[k] ~ dchisq(nu.sex.m[k])
    nu.sex.m[k] ~ dunif(0,100)
    tau.age.mk[k] ~ dchisq(nu.age.m[k])
    nu.age.m[k] ~ dunif(0,100)
    tau.educ.mk[k] ~ dchisq(nu.educ.m[k])
    nu.educ.m[k] ~ dunif(0,100)
  }

}